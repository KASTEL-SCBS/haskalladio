% BEGIN IMPORTS
:- import  exposesPhsicallyAccessiblePayloadTo/2,
           exposesPhsicallyAccessibleMetaDataTo/2 from linkModel.

:- import  dataAccessibleTo/2 from abstractAnalysis.

:- import  dataAllowedToBeAccessedBy/2,
           tamperingAbilities/2,
           locationsAccessibleBy/2,
           containerSecuredByMethod/2,
           furtherConnections/2,
           sharing/2,
           location/2,
           linkLocation/2
           from securityInstance.
% END IMPORTS

% BEGIN EXPORTS
:- export  % Predicates
           isInSecureWithRespectTo/1,               % isInSecureWithRespectTo(Attacker)

           containersFullyAccessibleBy/2,           % containersFullyAccessibleBy(Attacker,ResourceContainer)
           linksMetaDataFullyAccessibleBy/2,        % linksMetaDataFullyAccessibleBy(Attacker,LinkingResource)
           linksPayloadFullyAccessibleBy/2,         % linksPayloadFullyAccessibleBy(Attacker,LinkingResource)

           linksPhysicalAccessibleBy/2,             % linksPhysicalAccessibleBy(Attacker,Link)
           containersPhysicalAccessibleBy/2,        % containersPhysicalAccessibleBy(Attacker,Container)

           % Used Functors
           % Sharing = OpenShared | ControlledExclusive
           openShared/0, controlledExclusive/0,
           % FurtherConnections = Possible | Existing | Complete
           possible/0, existing/0, complete/0.
% END EXPORTS

justify_pred(isInSecureWithRespectTo(_)).
justify_pred(containersFullyAccessibleBy(_,_)).
justify_pred(linksMetaDataFullyAccessibleBy(_,_)).
justify_pred(linksPayloadFullyAccessibleBy(_,_)).
justify_pred(linksPhysicalAccessibleBy(_,_)).
justify_pred(containersPhysicalAccessibleBy(_,_)).

isInSecureWithRespectTo(Attacker) :-
        dataAccessibleTo(Attacker,Accessed),
        not (dataAllowedToBeAccessedBy(Attacker,Accessed)).


containersFullyAccessibleBy(Attacker,Container) :-
        containersPhysicalAccessibleBy(Attacker,Container),
        containerSecuredByMethod(Container,Method),
        tamperingAbilities(Attacker,Method).
containersFullyAccessibleBy(_,Container) :-
        sharing(Container,openShared),
        furtherConnections(Container,existing).
containersFullyAccessibleBy(Attacker,Container) :-
        containersPhysicalAccessibleBy(Attacker,Container),
        sharing(Container,openShared),
        furtherConnections(Container,possible).


linksPayloadFullyAccessibleBy(Attacker,Link) :-
    linksPhysicalAccessibleBy(Attacker,Link),
    exposesPhsicallyAccessiblePayloadTo(Link,Attacker).

linksMetaDataFullyAccessibleBy(Attacker,Link) :-
    linksPhysicalAccessibleBy(Attacker,Link),
    exposesPhsicallyAccessibleMetaDataTo(Link,Attacker).


linksPhysicalAccessibleBy(Attacker,Link) :-
    linkLocation(Link,Location),
    locationsAccessibleBy(Attacker,Location).

containersPhysicalAccessibleBy(Attacker,Container) :-
    location(Container,Location),
    locationsAccessibleBy(Attacker,Location).
