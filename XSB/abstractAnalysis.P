% BEGIN IMPORTS
:- import  requires/2,
           hasParameter/2,
           returnParameter/2,
           hasService/2
           from componentRepositoryInstance.
:- import  connects/3,
           componentOf/2,
           runsOn/2,
           systemAssembledTo/3,
           byAssembly/1
           from palladioInstance.

:- import  interfacesOn/3,
           parametersOf/2
           from palladio.

:- import  linksDataAccessibleBy/3,
           containersFullyAccessibleBy/2,
           providedInterfacesAccessibleTo/3,
           requiredInterfacesAccessibleTo/3
           from security.

:- import  includesCall/3,
           includes/3
           from securityInstance.
% END IMPORTS
% BEGIN EXPORTS
:- export  % Predicates
           accessibleParameters/3,               % accessibleParameters(Attacker,Parameter)
           observableServices/3,                 % observableServices(Attacker,Parameter)
           dataAccessibleTo/2.                   % dataAccessibleTo(Attacker,DataSet)
% END EXPORTS

justify_pred(accessibleParameters(_,_,_)).
justify_pred(observableServices(_,_,_)).
justify_pred(dataAccessibleTo(_,_)).

accessibleParameters(Attacker,Parameter,Context) :-
  % Ausgabe-Parameter, auf die der Angreifer als regulärer "Benutzer" des Systems Zugriff hat
  providedInterfacesAccessibleTo(Attacker,Interface,Context),
  hasService(Interface,Service),
  returnParameter(Service, Parameter).

accessibleParameters(Attacker,Parameter,Context) :-
  % Eingabe-Parameter,auf die der Angreifer als regulärer "Benutzer" des Systems Zugriff hat, weil er vom System Aufgerufen wird.
  requiredInterfacesAccessibleTo(Attacker,Interface,Context),
  hasService(Interface,Service),
  hasParameter(Service, Parameter).

accessibleParameters(Attacker,Parameter,Context) :-
  % Parameter, auf die der Angreifer Zugriff hat, weil er einen entsprechenden ResourceContainer angreifen konnte.
  containersFullyAccessibleBy(Attacker, Container),
  interfacesOn(Container,Interface,Context),
  hasService(Interface,Service),
  parametersOf(Service,Parameter).

accessibleParameters(Attacker,Parameter,Left) :-
  % Parameter, auf die der Angreifer Zugriff hat, weil er eine entsprechende LinkResource angreifen konnte.
  linksDataAccessibleBy(Attacker, Link, DataSet),
  connects(Link,ContainerLeft,ContainerRight),
  runsOn(Left,ContainerLeft),
  componentOf(Left,Component),
  requires(Component,Interface),
  systemAssembledTo(Left,Interface,byAssembly(Right)),
  runsOn(Right,ContainerRight),
  hasService(Interface, Service),
  parametersOf(Service,Parameter),
  includes(Parameter,DataSet,Left).


observableServices(Attacker, Service, Context) :-
  % Services, deren Aufrufe der Angreifer als regulärer "Benutzer" des Systems beobachten kann
  requiredInterfacesAccessibleTo(Attacker,Interface,Context),
  hasService(Interface, Service).

observableServices(Attacker,Service,Context) :-
  % Services, deren Aufrufe der Angreifer beobachten kann, weil er einen entsprechenden ResourceContainer angreifen konnte.
  containersFullyAccessibleBy(Attacker,Container),
  interfacesOn(Container,Interface, Context),
  hasService(Interface,Service).

observableServices(Attacker, Service, Left) :-
  % Services, deren Aufrufe der Angreifer beobachten kann, weil er eine entsprechende LinkResource angreifen konnte.
  linksDataAccessibleBy(Attacker, Link, DataSet),
  connects(Link,ContainerLeft, ContainerRight),
  runsOn(Left,ContainerLeft),
  componentOf(Left,Component),
  requires(Component,Interface),
  systemAssembledTo(Left,Interface,byAssembly(Right)),
  runsOn(Right,ContainerRight),
  hasService(Interface,Service),
  includesCall(Service,DataSet, Left).


dataAccessibleTo(Attacker,DataSet) :-
    accessibleParameters(Attacker,Parameter,Context),
    includes(Parameter,DataSet,Context).

dataAccessibleTo(Attacker,DataSet) :-
    observableServices(Attacker,Service,Context),
    includesCall(Service,DataSet,Context).
