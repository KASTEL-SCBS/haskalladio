% BEGIN IMPORTS
:- import  interfacesAllowedToBeUsedBy/2
           from securityInstance.


:- import  requires/2,
           inputParameters/2,
           outputParameters/2,
           services/2
           from componentRepositoryInstance.
:- import  linkBetween/3,
           componentOf/2,
           runsOn/2,
           systemProvides/1,
           systemRequires/1,
           systemAssembledTo/3,
           byAssembly/1
           from palladioInstance.

:- import  interfacesOn/2,
           parameters/2
           from palladio.

:- import  linksMetaDataFullyAccessibleBy/2,
           linksPayloadFullyAccessibleBy/2,
           containersFullyAccessibleBy/2
           from security.

:- import  classificationOfCall/2,
           classificationOf/2
           from securityInstance.
% END IMPORTS
% BEGIN EXPORTS
:- export  % Predicates
           accessibleParameters/2,               % accessibleParameters(Attacker,Parameter)
           observableServices/2,                 % observableServices(Attacker,Parameter)
           dataAccessibleTo/2.                   % dataAccessibleTo(Attacker,DataSet)
% END EXPORTS

justify_pred(accessibleParameters(_,_)).
justify_pred(observableServices(_,_)).
justify_pred(dataAccessibleTo(_,_)).

accessibleParameters(Attacker,Parameter) :-
  % Ausgabe-Parameter, auf die der Angreifer als regulärer "Benutzer" des Systems Zugriff hat
  interfacesAllowedToBeUsedBy(Attacker,Interface),
  systemProvides(Interface),
  services(Interface,Service),
  outputParameters(Service, Parameter).

accessibleParameters(Attacker,Parameter) :-
  % Eingabe-Parameter,auf die der Angreifer als regulärer "Benutzer" des Systems Zugriff hat, weil er vom System Aufgerufen wird.
  interfacesAllowedToBeUsedBy(Attacker, Interface),
  systemRequires(Interface),
  services(Interface,Service),
  inputParameters(Service, Parameter).

accessibleParameters(Attacker,Parameter) :-
  % Parameter, auf die der Angreifer Zugriff hat, weil er einen entsprechenden ResourceContainer angreifen konnte.
  containersFullyAccessibleBy(Attacker, Container),
  interfacesOn(Container,Interface),
  services(Interface,Service),
  parameters(Service,Parameter).

accessibleParameters(Attacker,Parameter) :-
  % Parameter, auf die der Angreifer Zugriff hat, weil er eine entsprechende LinkResource angreifen konnte.
  linksPayloadFullyAccessibleBy(Attacker, Link),
  linkBetween(Link,ContainerLeft,ContainerRight),
  runsOn(Left,ContainerLeft),
  componentOf(Left,Component),
  requires(Component,Interface),
  systemAssembledTo(Left,Interface,byAssembly(Right)),
  runsOn(Right,ContainerRight),
  services(Interface, Service),
  parameters(Service,Parameter).


observableServices(Attacker, Service) :-
  % Services, deren Aufrufe der Angreifer als regulärer "Benutzer" des Systems beobachten kann
  interfacesAllowedToBeUsedBy(Attacker,Interface),
  systemProvides(Interface),
  services(Interface, Service).

observableServices(Attacker,Service) :-
  % Services, deren Aufrufe der Angreifer beobachten kann, weil er einen entsprechenden ResourceContainer angreifen konnte.
  containersFullyAccessibleBy(Attacker,Container),
  interfacesOn(Container,Interface),
  services(Interface,Service).

observableServices(Attacker, Service) :-
  % Services, deren Aufrufe der Angreifer beobachten kann, weil er eine entsprechende LinkResource angreifen konnte.
  linksMetaDataFullyAccessibleBy(Attacker,Link),
  linkBetween(Link,ContainerLeft, ContainerRight),
  runsOn(Left,ContainerLeft),
  componentOf(Left,Component),
  requires(Component,Interface),
  systemAssembledTo(Left,Interface,byAssembly(Right)),
  runsOn(Right,ContainerRight),
  services(Interface,Service).


dataAccessibleTo(Attacker,DataSet) :-
    accessibleParameters(Attacker,Parameter),
    classificationOf(Parameter,DataSet).

dataAccessibleTo(Attacker,DataSet) :-
    observableServices(Attacker,Service),
    classificationOfCall(Service,DataSet).
