% BEGIN IMPORTS
:- import  requires/2,
           hasParameter/2,
           returnParameter/2,
           hasService/2
           from componentRepositoryInstance.
:- import  connects/3,
           componentOf/2,
           runsOn/2,
           systemAssembledTo/3,
           byAssembly/1
           from palladioInstance.

:- import  providedInterfacesOn/3,
           requiredInterfacesOn/3,
           parametersOf/2
           from palladio.

:- import  linksDataAccessibleBy/3,
           containersFullyAccessibleBy/2,
           providedInterfacesAccessibleTo/4,
           requiredInterfacesAccessibleTo/4
           from security.

:- import  includesCall/4,
           required/1,
	   provided/1,
           includes/4
           from securityInstance.
% END IMPORTS
% BEGIN EXPORTS
:- export  % Predicates
           accessibleParameters/5,               % accessibleParameters(Attacker,Parameter, Context, InterfaceWithRole, World)
           observableServices/5.                 % observableServices(Attacker,Parameter, Context, InterfaceWithRole,  World)

% END EXPORTS

justify_pred(accessibleParameters(_,_,_,_,_)).
justify_pred(observableServices(_,_,_,_,_)).

accessibleParameters(Attacker,Parameter,Context, provided(Interface), World) :-
  % Ausgabe-Parameter, auf die der Angreifer als regulärer "Benutzer" des Systems Zugriff hat
  providedInterfacesAccessibleTo(Attacker,Interface,Context, World),
  hasService(Interface,Service),
  returnParameter(Service, Parameter).

accessibleParameters(Attacker,Parameter,Context, required(Interface), World) :-
  % Eingabe-Parameter,auf die der Angreifer als regulärer "Benutzer" des Systems Zugriff hat, weil er vom System Aufgerufen wird.
  requiredInterfacesAccessibleTo(Attacker,Interface,Context, World),
  hasService(Interface,Service),
  hasParameter(Service, Parameter).

accessibleParameters(Attacker,Parameter,Context, provided(Interface), _World) :-
  % Parameter, auf die der Angreifer Zugriff hat, weil er einen entsprechenden ResourceContainer angreifen konnte.
  containersFullyAccessibleBy(Attacker, Container),
  providedInterfacesOn(Container,Interface,Context),
  hasService(Interface,Service),
  parametersOf(Service,Parameter).
accessibleParameters(Attacker,Parameter,Context, required(Interface), _World) :-
  % Parameter, auf die der Angreifer Zugriff hat, weil er einen entsprechenden ResourceContainer angreifen konnte.
  containersFullyAccessibleBy(Attacker, Container),
  requiredInterfacesOn(Container,Interface,Context),
  hasService(Interface,Service),
  parametersOf(Service,Parameter).

accessibleParameters(Attacker,Parameter,Left, required(Interface), World) :-
  % Parameter, auf die der Angreifer Zugriff hat, weil er eine entsprechende LinkResource angreifen konnte.
  linksDataAccessibleBy(Attacker, Link, DataSet),
  connects(Link,ContainerLeft,ContainerRight),
  runsOn(Left,ContainerLeft),
  componentOf(Left,Component),
  requires(Component,Interface),
  systemAssembledTo(Left,Interface,byAssembly(Right)),
  runsOn(Right,ContainerRight),
  hasService(Interface, Service),
  parametersOf(Service,Parameter),
  includes(Parameter,DataSet,Left, World).


observableServices(Attacker, Service, Context, required(Interface), World) :-
  % Services, deren Aufrufe der Angreifer als regulärer "Benutzer" des Systems beobachten kann
  requiredInterfacesAccessibleTo(Attacker,Interface,Context, World),
  hasService(Interface, Service).

observableServices(Attacker,Service,Context, required(Interface), _World) :-
  % Services, deren Aufrufe der Angreifer beobachten kann, weil er einen entsprechenden ResourceContainer angreifen konnte.
  containersFullyAccessibleBy(Attacker,Container),
  requiredInterfacesOn(Container,Interface, Context),
  hasService(Interface,Service).
observableServices(Attacker,Service,Context, provided(Interface), _World) :-
  % Services, deren Aufrufe der Angreifer beobachten kann, weil er einen entsprechenden ResourceContainer angreifen konnte.
  containersFullyAccessibleBy(Attacker,Container),
  providedInterfacesOn(Container,Interface, Context),
  hasService(Interface,Service).


observableServices(Attacker, Service, Left, required(Interface), World) :-
  % Services, deren Aufrufe der Angreifer beobachten kann, weil er eine entsprechende LinkResource angreifen konnte.
  linksDataAccessibleBy(Attacker, Link, DataSet),
  connects(Link,ContainerLeft, ContainerRight),
  runsOn(Left,ContainerLeft),
  componentOf(Left,Component),
  requires(Component,Interface),
  systemAssembledTo(Left,Interface,byAssembly(Right)),
  runsOn(Right,ContainerRight),
  hasService(Interface,Service),
  includesCall(Service,DataSet, Left, World).

