% BEGIN IMPORTS
:- import  exposesPhysicallyAccessibleDataTo/3 from linkModel.

:- import  dataAllowedToBeAccessedBy/2,
           tamperingAbilities/3,
           locationsAccessibleBy/2,
           furtherConnections/2,
           sharingOf/2,
           location/3,
           classificationOfCall/2,
           classificationOf/2,
           uiInterfaceOn/2,
           linkLocation/3
           from securityInstance.


:- import  accessibleParameters/2,
           observableServices/2
           from abstractAnalysis.

:- import  providedInterfacesOn/2,
           requiredInterfacesOn/2
           from palladio.
% END IMPORTS

% BEGIN EXPORTS
:- export  % Predicates
           isInSecureWithRespectTo/1,               % isInSecureWithRespectTo(Attacker)

           containersFullyAccessibleBy/2,           % containersFullyAccessibleBy(Attacker,ResourceContainer)
           linksDataAccessibleBy/3,                 % linksDataAccessibleBy(Attacker,LinkingResource, Dataset)

           linksPhysicalAccessibleBy/3,             % linksPhysicalAccessibleBy(Attacker,Link,Location)
           containersPhysicalAccessibleBy/3,        % containersPhysicalAccessibleBy(Attacker,Container,Location)

           providedInterfacesAccessibleTo/2,        % providedInterfacesAccessibleTo(Attacker, Interface)
           requiredInterfacesAccessibleTo/2,        % requiredInterfacesAccessibleTo(Attacker, Interface)

           % Used Functors
           % Sharing = OpenShared | ControlledExclusive
           shared/0, exclusive/0,
           % FurtherConnections = Possible | Existing | Complete
           possible/0, existing/0, complete/0,
           % Default tampering Abiility
           none/0.
% END EXPORTS

justify_pred(isInSecureWithRespectTo(_)).
justify_pred(containersFullyAccessibleBy(_,_)).
justify_pred(linksDataAccessibleBy(_,_,_)).
justify_pred(linksPhysicalAccessibleBy(_,_,_)).
justify_pred(containersPhysicalAccessibleBy(_,_,_)).
justify_pred(providedInterfacesAccessibleTo(_,_)).
justify_pred(requiredInterfacesAccessibleTo(_,_)).
justify_pred(serviceAllowedToBeObservedBy(_,_)).
justify_pred(parameterAllowedToBeAccessedBy(_,_)).

isInSecureWithRespectTo(Attacker) :-
        accessibleParameters(Attacker,Parameter),
        not(parameterAllowedToBeAccessedBy(Attacker,Parameter)).

isInSecureWithRespectTo(Attacker) :-
        observableServices(Attacker,Service),
        not(serviceAllowedToBeObservedBy(Attacker,Service)).


serviceAllowedToBeObservedBy(Attacker, Service) :-
        classificationOfCall(Service, DataSet),
        dataAllowedToBeAccessedBy(Attacker, DataSet).


parameterAllowedToBeAccessedBy(Attacker, Parameter) :-
        classificationOf(Parameter, DataSet),
        dataAllowedToBeAccessedBy(Attacker, DataSet).


containersFullyAccessibleBy(Attacker,Container) :-
        containersPhysicalAccessibleBy(Attacker,Container,Location),
        location(Container,Location,Method),
        tamperingAbilities(Attacker,Location,Method).
containersFullyAccessibleBy(_,Container) :-
        sharingOf(Container,openShared),
        furtherConnections(Container,existing).
containersFullyAccessibleBy(Attacker,Container) :-
        containersPhysicalAccessibleBy(Attacker,Container,_),
        sharingOf(Container,openShared),
        furtherConnections(Container,possible).


linksDataAccessibleBy(Attacker,Link,Dataset) :-
    linksPhysicalAccessibleBy(Attacker,Link,Location),
    linkLocation(Link,Location,Method),
    tamperingAbilities(Attacker,Location,Method),
    exposesPhysicallyAccessibleDataTo(Link,Attacker,Dataset).


linksPhysicalAccessibleBy(Attacker,Link,Location) :-
    linkLocation(Link,Location,_),
    locationsAccessibleBy(Attacker,Location).

containersPhysicalAccessibleBy(Attacker,Container,Location) :-
    location(Container,Location,_),
    locationsAccessibleBy(Attacker,Location).

providedInterfacesAccessibleTo(Attacker, Interface) :-
    containersPhysicalAccessibleBy(Attacker,Container,_),
    providedInterfacesOn(Container, Interface),
    uiInterfaceOn(Container, Interface).

requiredInterfacesAccessibleTo(Attacker, Interface) :-
    containersPhysicalAccessibleBy(Attacker,Container,_),
    requiredInterfacesOn(Container, Interface),
    uiInterfaceOn(Container, Interface).
