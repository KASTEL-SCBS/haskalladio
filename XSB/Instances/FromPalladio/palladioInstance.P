% BEGIN IMPORTS
:- import  encapsulatedComponent/2,
           assemblyContext/1,
           providingAssemblyContext/2,
           requiringAssemblyContext/2,
           assemblyConnector/1,
           connectedResourceContainers/2,
           linkingResource/1,
           providedInterface/2,
           operationProvidedRole/1,
           operationRequiredRole/1,
           outerRequiredRole/2,
           outerProvidedRole/2,
           innerProvidedRole/2,
           innerRequiredRole/2,
           providedRole/2,
           requiredRole/2,
           requiredInterface/2,
           assemblyContext/2,
           assemblyContext_AllocationContext/2,
           providedDelegationConnector/1,
           requiredDelegationConnector/1,
           resourceContainer/1,
           resourceContainer/2,
           allocationContext/1
           from fromPalladio.

:- import  provides/2,
           requires/2
           from componentRepositoryInstance.
% END IMPORTS
justify_pred(connects(_,_,_)).

% % resourceContainer/2 and linkingResources/2 are appearantly redundant,
% % hence we do not use the following 2 rules, but the 2 simpler rules thereafter
% resourcecontainers(RC) :- resourceContainer(_Environment, Containers), member(RC, Containers).
% linkingresources(LR) :- linkingResources(_Environment, Links), member(LR, Links).

linkingresources(LR) :- linkingResource(LR).
resourcecontainers(RC) :- resourceContainer(RC).

system(Context) :- assemblyContext(Context).

systemProvides(Interface) :- systemProvidesAssembledTo(Interface,_).

systemRequires(Interface) :- systemRequiredAssembledTo(Interface,_).

systemAssembledTo(RequiringContext, Interface, byAssembly(ProvidingContext)) :-
        assemblyConnector(Connector),
        requiringAssemblyContext(Connector, RequiringContext),
        providingAssemblyContext(Connector, ProvidingContext),
        requiredRole(Connector, RequiredRole),
        requiredInterface(RequiredRole, Interface),
        providedRole(Connector, ProvidedRole),        % By wellformedness of exported models, thes
        providedInterface(ProvidedRole, Interface).   % two subgoals are presumably redundant.


systemProvidesAssembledTo(Interface, Context) :-
        providedDelegationConnector(Connector),
        assemblyContext(Connector,Context),

        innerProvidedRole(Connector,InnerProvidedRole),
        operationProvidedRole(InnerProvidedRole), % TODO: redundant?
        providedInterface(InnerProvidedRole,Interface),

        % TODO: redundant?
        outerProvidedRole(Connector,OuterProvidedRole),
        operationProvidedRole(OuterProvidedRole), % TODO: redundant?
        providedInterface(OuterProvidedRole,Interface).


systemRequiredAssembledTo(Interface, Context) :-
        requiredDelegationConnector(Connector),
        assemblyContext(Connector,Context),

        innerRequiredRole(Connector,InnerRequiredRole),
        operationRequiredRole(InnerRequiredRole), % TODO: redundant?
        requiredInterface(InnerRequiredRole,Interface),

        % TODO: redundant?
        outerRequiredRole(Connector,OuterRequiredRole),
        operationRequiredRole(OuterRequiredRole), % TODO: redundant?
        requiredInterface(OuterRequiredRole,Interface).


runsOn(AssemblyContext, ResourceContainer) :-
        allocationContext(AllocationContext), % TODO: likely reduntant
        resourceContainer(AllocationContext,ResourceContainer),
        assemblyContext_AllocationContext(AllocationContext,AssemblyContext).

componentOf(AssemblyContext, Component) :-
        assemblyContext(AssemblyContext), % TODO: likely redundant
        encapsulatedComponent(AssemblyContext,Component).

connects(Link,RCLeft,RCRight) :- connectedResourceContainers(Link,[RCLeft,RCRight]).
connects(Link,RCLeft,RCRight) :- connectedResourceContainers(Link,[RCRight,RCLeft]).
